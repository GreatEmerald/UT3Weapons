/******************************************************************************
UT3TranslocatorFire

Creation date: 2008-07-14 12:18
Last change: $Id$
Copyright (c) 2008, Wormbo
******************************************************************************/

class UT3TranslocatorFire extends TransFire;


//=============================================================================
// Imports
//=============================================================================

#exec obj load file=WeaponSounds.uax


//=============================================================================
// Properties
//=============================================================================

var array<class<TransBeacon> > TeamProjectileClasses;
var Sound RecallFailedSound;


function projectile SpawnProjectile(Vector Start, Rotator Dir)
{
    local TransBeacon TransBeacon;

    if (TransLauncher(Weapon).TransBeacon == None) {
		if (Instigator != None && Instigator.PlayerReplicationInfo != None && Instigator.PlayerReplicationInfo.Team != None && Instigator.PlayerReplicationInfo.Team.TeamIndex < TeamProjectileClasses.Length)
			TransBeacon = Weapon.Spawn(TeamProjectileClasses[Instigator.PlayerReplicationInfo.Team.TeamIndex],,, Start, Dir);
		else
			TransBeacon = TransBeacon(Weapon.Spawn(ProjectileClass,,, Start, Dir));
        TransLauncher(Weapon).TransBeacon = TransBeacon;
        Weapon.PlaySound(TransFireSound, SLOT_Interact,,,,, false);
    } else {
        TransLauncher(Weapon).ViewPlayer();
        if (TransLauncher(Weapon).TransBeacon.Disrupted()) {
			if (Instigator != None && PlayerController(Instigator.Controller) != None)
				PlayerController(Instigator.Controller).ClientPlaySound(RecallFailedSound);
		} else {
			TransLauncher(Weapon).TransBeacon.Destroy();
			TransLauncher(Weapon).TransBeacon = None;
			Weapon.PlaySound(RecallFireSound, SLOT_Interact,,,,, false);
		}
    }
    return TransBeacon;
}


//=============================================================================
// Default values
//=============================================================================

defaultproperties
{
	TransFireSound    = ProceduralSound'TranslocatorFire'
	RecallFireSound   = SoundGroup'TranslocatorRecall'
	RecallFailedSound = Sound'BSeekLost1'
	
	ProjectileClass = class'UT3TranslocatorDiskRed'
	TeamProjectileClasses[0] = class'UT3TranslocatorDiskRed'
	TeamProjectileClasses[1] = class'UT3TranslocatorDiskBlue'
}
